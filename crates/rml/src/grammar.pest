WHITESPACE    = _{ " " | "\t" | "\r" | "\n" | COMMENT_MULTI | LINE_COMMENT }
COMMENT_MULTI = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
LINE_COMMENT  = _{ "//" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }

// Основной файл UI
file = { SOI ~ directive* ~ element* ~ directive* ~ EOI }

ident    = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
ns_ident =  { ident ~ ("::" ~ ident)* }
alias    =  { "@" ~ ident }

integer = @{ "-"? ~ ASCII_DIGIT+ }
float   = @{ integer ~ ("." ~ integer*)? }
number  = @{ float | integer }

boolean          =  { "true" | "false" }
string           = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
enum_val         = @{ (ident | number_with_unit) }
number_with_unit =  { ASCII_DIGIT+ ~ (!(WHITESPACE | "/") ~ ANY)* }

structure    =  { "{{" ~ struct_field* ~ "}}" }
struct_field =  { WHITESPACE* ~ field_name ~ "=" ~ field_value ~ ("," ~ WHITESPACE*)? }
field_name   = @{ ASCII_ALPHANUMERIC+ }
field_value  =  { string | number | boolean | enum_val }

directive_content = { (!">" ~ ANY)* }
directive         = { "#" ~ ident ~ ("<" ~ directive_content ~ ">")? }

element = { empty_tag | tag }

empty_tag = { "<" ~ ns_ident ~ alias? ~ attribute* ~ "/>" }
tag       = {
    "<" ~ ns_ident ~ alias? ~ attribute* ~ ">" ~ element* ~ "</" ~ ns_ident ~ ">"
}

// Attributes
attr_value = { boolean | enum_val | number | string | expression | structure }
attribute  = { ident ~ "=" ~ attr_value }

// Expression {ExpressionName arg1=val1, arg2=val2}
expression =  { "{" ~ ns_ident ~ (WHITESPACE* ~ expr_arg)* ~ "}" }
expr_arg   =  { arg_name ~ "=" ~ arg_val }
arg_name   = @{ ASCII_ALPHANUMERIC+ }
arg_val    =  { string | number | boolean | enum_val | list_val }

list_val = { "[" ~ (arg_val ~ ("," ~ arg_val)*)? ~ "]" }