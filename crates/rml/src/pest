WHITESPACE    = _{ " " | "\t" | "\r" | "\n" | COMMENT_MULTI | LINE_COMMENT}
COMMENT_MULTI = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
LINE_COMMENT  = _{ "//" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }

// Основной файл UI
file        = { SOI ~ directive* ~ element* ~ directive* ~ EOI }

identifier  = @{ ASCII_ALPHANUMERIC+ }
number      = @{ ASCII_DIGIT+ }
boolean     = { "true" | "false" }
string      = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
enum_val    = @{ (ASCII_ALPHANUMERIC+ | number_with_unit) }
number_with_unit = { ASCII_DIGIT+ ~ ASCII_ALPHANUMERIC+ }

structure   = { "{{" ~ struct_field* ~ "}}" }
struct_field = { WHITESPACE* ~ field_name ~ "=" ~ arg_val ~ ("," ~ WHITESPACE*)? }
field_name  = @{ ASCII_ALPHANUMERIC+ }

directive_content = { (!">" ~ ANY)* }
directive = { "#" ~ identifier ~ ("<" ~ directive_content ~ ">")? }

element = { empty_tag | tag }

empty_tag = { "<" ~ identifier ~ attribute* ~ "/>" }
tag = { 
	"<" ~ identifier ~ attribute* ~ ">" ~
    element* ~
    "</" ~ identifier ~ ">"
}

//Attributes
attr_value  = { enum_val | number | boolean | string | expression | structure }
attribute = { identifier ~ "=" ~ attr_value } 

// Expression {ExpressionName arg1=val1, arg2=val2}
expression  = { "{" ~ expr_name ~ (WHITESPACE* ~ expr_arg)* ~ "}" }
expr_name   = @{ ASCII_ALPHANUMERIC+ }
expr_arg    = { arg_name ~ "=" ~ arg_val }
arg_name    = @{ ASCII_ALPHANUMERIC+ }
arg_val     = { string | number | boolean | enum_val | list_val | negation }

list_val    = { "[" ~ (arg_val ~ ("," ~ arg_val)*)? ~ "]" }
negation    = { "!" ~ arg_val }
